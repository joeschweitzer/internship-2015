buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.2.3.RELEASE'
		classpath 'org.springframework:springloaded:1.2.3.RELEASE'
	}
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'spring-boot'
apply plugin: 'eclipse'
apply plugin: 'maven'

repositories {
	mavenCentral()
	flatDir {
		dirs 'lib'
	}
}

dependencies {
	compile 'org.apache.tomcat.embed:tomcat-embed-core'
	compile 'org.apache.tomcat.embed:tomcat-embed-jasper'
	compile 'org.codehaus.groovy:groovy-all:2.4.3'
	compile 'org.codehaus.groovy.modules.http-builder:http-builder:0.7.1'
	compile 'org.springframework.boot:spring-boot-starter-web'	
	compile 'org.springframework.boot:spring-boot-starter-jdbc'
	compile 'org.postgresql:postgresql:9.4-1201-jdbc41'	
	
	
	testCompile 'org.hamcrest:hamcrest-all:1.3'
	testCompile 'org.springframework.boot:spring-boot-starter-test'
	testCompile 'org.springframework:spring-test:4.1.1.RELEASE'
	
	
	runtime 'org.springframework:springloaded:1.2.3.RELEASE'
}

eclipse {
	pathVariables 'GRADLE_USER_HOME': gradle.gradleUserHomeDir
}

sourceSets {
	main {
		java {
			srcDir 'src/java'
		}
		groovy {
			srcDir 'src/groovy'
		}
		resources {
			srcDir 'src/resources'
		}
	
		output.classesDir = 'build/classes'
	}
	test {
		java {
			srcDir 'test/java'
		}
		groovy {
			srcDir 'test/groovy'
		}
		resources {
			srcDir 'test/resources'
		}
		
		output.classesDir = 'build/classes'
	}
}

run {
	main 'datawalks.Application'
}

bootRun {
	maxHeapSize '512m'
	systemProperties = [
        "java.net.preferIPv4Stack": "true",
        "user": System.getProperty("user"), 
        "password": System.getProperty("password")
    ]
}

group = 'datawalks'
version = '1.0'
archivesBaseName = 'datawalks'

uploadArchives {
	repositories {
		mavenDeployer {
			repository(url: "http://192.168.99.100:32770/artifactory/libs-release-local") {
				authentication(userName: "xyz", password: "xyz")
			}
			pom {
				project {
					parent {
						groupId "org.springframework.boot"
						artifactId "spring-boot-starter-parent"
						version "1.2.3.RELEASE"
					}
				}
			}
        }
    }
}
